module LC.Reasoner where

import Ideas.Common.Library hiding (many)
import Ideas.Main.Default

import LC.Syntax

-- This module contains the definition of the Domain Reasoner,
-- including rule and strategy definitions.

-- Named with a ' to avoid clashing with the Exercise field name.
examples' :: Examples Expr
examples' = examplesWithDifficulty $ easy <> medium <> hard
  where
    easy =
      (Easy,) . parseKnown <$>
      [ "(位x. x) y"
      , "(位x. x) (位y. y)"
      , "(位x y. y x) z w"
      , "(位x. x x) (位x. x)"
      ]

    medium =
      (Medium,) . parseKnown <$>
      [ "(位x y. y) (位x. y)"
      , "(位a. a) (位x. (位y. y) z ((位x. x) w))"
      , "(位x. x ((位y. y) z)) w"
      , "(位f x. f (f x)) x"
      , "(位f x y. f (f x y)) x y"
      , "(位x. x x) ((位y. y y) z)"
      ]

    hard =
      (Difficult,) . parseKnown <$>
      [ "(位Z S. S (S Z)) (位f z. z) (位n f z. f (n f z))" -- Evaluate 2 in Church numerals
      , "(位Z S +. + (S (S Z)) (S (S Z))) (位f z. z) (位n f z. f (n f z)) (位m n f z. m f (n f z))" -- Evaluate 2 + 2 in Church numerals
      , "(位Z S *. * (S (S (S Z))) (S (S Z))) (位f z. z) (位n f z. f (n f z)) (位m n f z. m (n f) z)" -- Evaluate 3 * 2 in Church numerals
      , "(位Z S + *. + (S Z) (* (S (S Z)) (S (S (S Z))))) (位f z. z) (位n f z. f (n f z)) (位m n f z. m f (n f z)) (位m n f z. m (n f) z)" -- Evaluate 1 + 2 * 3 in Church numerals
      , "(位x. x x) (位.  )"
      ]

-- Apply a 尾-reduction, if no 伪-renaming is needed.
尾Rule :: Rule Expr
尾Rule =
  describe "尾-reduce" $
    makeRule "lc.beta" \case
      App (Lam v e) a -> trySubst v a e
      _ -> Nothing

-- If a 尾-reduction fails due to capturing during substitution,
-- 伪-rename the subterm that caused the failure.
伪Rule :: Rule Expr
伪Rule =
  describe "伪-rename" $
    makeRule "lc.alpha" \case
      App (Lam v e) a | Nothing <- trySubst v a e -> Just $ App (Lam v $ 伪Rename v a e) a
      _ -> Nothing

-- Buggy 尾-reduction which changes the meaning of the expression.
尾RuleBuggy :: Rule Expr
尾RuleBuggy =
  describe "尾-reduce (buggy)" $
    setBuggy True $
      makeRule "lc.beta.buggy" \case
        App (Lam v e) a | Nothing <- trySubst v a e -> Just $ substBuggy v a e
        _ -> Nothing

-- As per https://en.wikipedia.org/wiki/Lambda_calculus#Reduction_strategies
-- Normal order: "The leftmost outermost redex is reduced first."
-- Appliative order: "The leftmost innermost redex is reduced first."

-- The 'outermost' and 'innermost' combinators do exactly what we need,
-- so the strategies are easy to define.

normalOrderStrategy :: LabeledStrategy (Context Expr)
normalOrderStrategy =
  label "lc.norm"
  $ outermost $ liftToContext $ 尾RuleBuggy .|. 尾Rule |> 伪Rule

applicativeOrderStrategy :: LabeledStrategy (Context Expr)
applicativeOrderStrategy =
  label "lc.app"
  $ innermost $ liftToContext $ 尾RuleBuggy .|. 尾Rule |> 伪Rule

normalForms :: [(String, String, Expr -> Bool)]
normalForms =
  [ ("nf", "normal form", isNormalForm)
  , ("hnf", "head normal form", isHeadNormalForm)
  , ("whnf", "weak-head normal form", isWeakHeadNormalForm)
  ]

reductionOrders ::[(String, String, LabeledStrategy (Context Expr))]
reductionOrders =
  [ ("norm", "normal order", normalOrderStrategy)
  , ("app", "applicative order", applicativeOrderStrategy)
  ]

mkExercise :: (String, String, Expr -> Bool) -> (String, String, LabeledStrategy (Context Expr)) -> Exercise Expr
mkExercise (nfId, nfName, nfPredicate) (orderId, orderName, orderStrategy) =
  emptyExercise
  { exerciseId    = describe
                      ("Reduce a 位-term to " <> nfName <> ", using " <> orderName <> " reduction")
                      (newId $ "lc." <> nfId <> "." <> orderId)
  , status        = Experimental
  , strategy      = orderStrategy
  , prettyPrinter = prettyPrintExpr
  , navigation    = termNavigator
  , parser        = parseExpr
  , equivalence   = withoutContext 伪尾Equiv
  , similarity    = withoutContext 伪Equiv
  , ready         = predicate nfPredicate
  , examples      = examples'
  }

exercises' :: [Exercise Expr]
exercises' = mkExercise <$> normalForms <*> reductionOrders

reasoner :: DomainReasoner
reasoner =
  describe "Domain reasoner for 位-calculus"
    (newDomainReasoner "lc")
    { exercises = Some <$> exercises'
    , services  = metaServiceList reasoner <> serviceList
    }

runDomainReasoner :: IO ()
runDomainReasoner = defaultMain reasoner
